plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.2.1'
    id 'signing'
    id 'java-library-distribution'
    id 'com.github.jk1.dependency-license-report' version '1.16'
}

description = "Jefferson Lab Alarm Warning System Java Library"
group 'org.jlab'
version '4.3.0'

ext.releaseDate = 'May 25 2022'

tasks.withType(JavaCompile) {
    options.release = 11
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

java {
    withJavadocJar()
    withSourcesJar()
}

avro {
    fieldVisibility = "PRIVATE"
}

task avroJar(type: Jar) {
    classifier 'avro'
    from 'src/main/avro'
}

repositories {
    mavenCentral()

    maven {
        url "https://s01.oss.sonatype.org/content/groups/public/"
    }

    maven {
        url "https://packages.confluent.io/maven"
    }
}

sourceSets {
    integration {
        java.srcDir "${projectDir}/src/integration/java"
        resources.srcDir "${projectDir}/src/integration/resources"
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    testImplementation.extendsFrom implementation
    integrationImplementation.extendsFrom testImplementation
    integrationRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
    api "org.jlab:kafka-common:1.1.0"
    implementation "org.apache.kafka:kafka-clients:3.1.0",
            "org.apache.avro:avro:1.11.0",
            "org.slf4j:slf4j-log4j12:1.7.36",
            "io.confluent:kafka-avro-serializer:7.1.1"
    testImplementation "junit:junit:4.13.2"
}

task integrationTest(type: Test) {
    description = 'Runs integration tests using existing BOOTSTRAP_SERVERS env or test default (localhost:9094).'
    group = 'verification'

    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
        showStandardStreams = true
    }
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

processIntegrationResources {
    duplicatesStrategy 'include'
}

processResources {
    filesMatching('release.properties') {
        filter {
            String line -> line.replaceAll("@VERSION@", version)
        }
        filter {
            String line -> line.replaceAll("@RELEASE_DATE@", releaseDate)
        }
    }
}

javadoc {
    options.overview = "src/main/java/overview.html"
    options.source = 11
    options.with {
        links 'https://docs.oracle.com/en/java/javase/11/docs/api/'
    }
    options.addStringOption('Xdoclint/package:-org.jlab.jaws.entity', '-quiet')
}


build {
    dependsOn(installDist)
}

installDist.dependsOn(generateLicenseReport)
distTar.dependsOn(generateLicenseReport)
distZip.dependsOn(generateLicenseReport)

distributions {
    main {
        contents {
            from("build/reports/dependency-license") {
                into "dependency-license"
            }
            from('config') {
                into 'config'
            }
        }
    }
}

signing {
    sign publishing.publications
}


publishing {
    repositories {
        maven {
            name "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username = project.findProperty("ossrh.user") ?: System.getenv("OSSRH_USER")
                password = project.findProperty("ossrh.token") ?: System.getenv("OSSRH_TOKEN")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/JeffersonLab/jaws-libj")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.token") ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from(components.java)
            pom {
                name = project.name
                description = project.description
                url = "https://github.com/JeffersonLab/jaws-libj"
                licenses {
                    license {
                        name = "The MIT License"
                        url = "https://github.com/JeffersonLab/jaws-libj/blob/master/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "slominskir"
                        name = "Ryan Slominski"
                        email = "ryans@jlab.org"
                    }
                }
                scm {
                    url = "https://github.com/JeffersonLab/jaws-libj.git"
                }
            }
        }
    }
    artifacts {
        archives javadocJar, sourcesJar, avroJar
    }
}