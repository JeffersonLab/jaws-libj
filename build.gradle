plugins {
    id 'java-library'
    id 'maven-publish'
}

group 'org.jlab.alarms'
version '0.1.0'

ext.confluentVer = '6.0.1'
ext.kafkaVer = '2.6.0'

tasks.withType(JavaCompile) {
    options.compilerArgs += ["-Xlint:unchecked"]
}

repositories {
    mavenCentral()

    maven {
        url "https://packages.confluent.io/maven"
    }
}

dependencies {
    implementation "org.apache.kafka:kafka-streams:" + kafkaVer,
            "io.confluent:kafka-streams-avro-serde:" + confluentVer,
            "org.apache.avro:avro:1.10.1",
            "org.slf4j:slf4j-log4j12:1.7.30"
    testImplementation "junit:junit:4.13.1"
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

jar {
    archiveFileName = 'jaws-libj.jar'
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/JeffersonLab/kafka-common-java")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
    publications {
        gpr(MavenPublication) {
            from(components.java)
        }
    }
}